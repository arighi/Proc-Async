#!/usr/bin/env perl
#
#   Usage: ./extester --help
#
#   Martin Senger <martin.senger@gmail.com>
#   July 2012
#
# ABSTRACT: mock external program
# PODNAME:  extester
# -----------------------------------------------------------------

use warnings;
use strict;

our $VERSION;

# -----------------------------------------------------------------
# Command-line arguments and script usage
# -----------------------------------------------------------------
my ($opt_stdout, $opt_stderr, $opt_exit, $opt_signal, $opt_sleep, $opt_progress);
BEGIN {
    use Getopt::Long;

    $VERSION = '1.0.0';

    Getopt::Long::Configure ('no_ignore_case');
    GetOptions ( 'h|help'     => sub { exec ('perldoc', $0);
				       die "Sorry, 'perldoc' not found\n"; },
                 'v|version'  => sub { print "$VERSION\n";
				       exit (0); },

                 #
                 'stdout=s'   => \$opt_stdout,
                 'stderr=s'   => \$opt_stderr,
                 'exit=i'     => \$opt_exit,
                 'signal=i'   => \$opt_signal,
                 'sleep=i'    => \$opt_sleep,
                 'progress=i' => \$opt_progress,

        ) or exit 1;
}  # end of BEGIN

# -----------------------------------------------------------------

$| = 1;  # flushing STDOUT

# check arguments
if (defined $opt_sleep) {
    $opt_sleep = - $opt_sleep if $opt_sleep < 0;   # only non-negative sleep
    if (defined $opt_progress) {
	$opt_progress = - $opt_progress if $opt_progress < 0;   # only non-negative progress
	if ($opt_progress > $opt_sleep) {
	    warn "EXTESTER: -progress $opt_progress > -sleep $opt_sleep. -progress ignored.\n";
	    undef $opt_progress;
	}
    }
} elsif (defined $opt_progress) {
    warn "EXTESTER: -progress valid only with -sleep. -progress ignored.\n";
    undef $opt_progress;
}

# the main
print STDOUT "$opt_stdout\n" if defined $opt_stdout;
print STDERR "$opt_stderr\n" if defined $opt_stderr;

if ($opt_progress) {
    my $slept = $opt_sleep;
    while ($slept > 0) {
	sleep ($opt_progress);
	progress_report();
	$slept -= $opt_progress;
    }
} elsif (defined $opt_sleep) {
    sleep ($opt_sleep);    
}

kill ($opt_signal => $$) if $opt_signal;
exit (defined $opt_exit ? $opt_exit : 0);

sub progress_report {
    print STDERR "Progress reported at ", scalar localtime, "\n";
}
__END__

=head1 SYNOPSIS

   extester [-stdout <text>]  [-stderr <text>]        \
            [-exit <integer>] [-signal <integer>]     \
            [-sleep <seconds> [-progress <seconds>] ]

   extester -h
   extester -help
   extester -man
   extester -version


=head1 DESCRIPTION

A simple script that does nothing except that:

=over

=item * it may print a given text to its standard output

=item * it may print a given text to its standard error

=item * it may exit with a given exit code

=item * it may send to itself a given signal (which may cause exiting)

=item * it may sleep for a given time period before exiting (or signaling)

=item * it may print a short progress report on its standard error every
given interval

=back

=head1 OPTIONS

=head2 General options

=over

=item B<-h>

Print a brief usage message and exits.

=item B<-help>

Print a brief usage message with options and exits.

=item B<-man>

Print a full usage message and exits.

=item B<-version>

Print the version and exits.

=back

=cut
